plugins {
    id 'java'
    id 'org.springframework.boot' version "${springBootPluginVersion}"
    id 'io.spring.dependency-management' version "${springDependencyManagementVersion}"
    id 'com.google.cloud.tools.jib' version "${jibPluginVersion}"
}

group = 'store.shportfolio'
version = '1.0.0'

repositories {
    mavenCentral()
}

dependencies {
    testImplementation platform("org.junit:junit-bom:${junitBomVersion}")

    implementation project(':common:common-domain')
    implementation project(':deploy-service:deploy-domain')
    implementation project(':deploy-service:deploy-application')
    implementation project(':deploy-service:deploy-infrastructure')

    implementation "org.springframework.cloud:spring-cloud-starter-netflix-eureka-client:${springCloudStarterVersion}"
    implementation "org.springframework.cloud:spring-cloud-starter"

    testImplementation 'org.junit.jupiter:junit-jupiter'
}

test {
    useJUnitPlatform()
}

tasks.withType(JavaCompile) {
    options.compilerArgs << "-parameters"
}

jar {
    manifest {
        attributes 'Main-Class': 'store.shportfolio.deploy.container.DeployApplication' // 메인 클래스의 완전한 패키지 이름
    }
    enabled = true
}

jib {
    from {
        image = 'bellsoft/liberica-openjre-alpine:17.0.15'
        platforms {
            platform {
                architecture = 'arm64'
                os = 'linux'
            }
        }
    }
    to {
        image = 'deploy-service'
        tags = ['1.0.0']
    }
    container {
        mainClass = 'store.shportfolio.deploy.container.DeployApplication'
        jvmFlags = ['-Dspring.profiles.active=dev']
        ports = ['9030']
        creationTime = 'USE_CURRENT_TIMESTAMP'
    }
}
